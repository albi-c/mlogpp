########################
# Mining script v1.2   #
#                      #
# Connect:             #
# - hail [hail1]       #
# - mem cell [cell1]   #
# - switch [switch1]   #
# - message [message1] #
########################

UnitType UNIT = @mono
Block VAULT = vault1
num DEPOSIT_FLAG = floor(@thisx + @thisy * @mapw)

num x = 0
num y = 0

print("[orange]Miner v1.2\nEnable switch and click\n on an ore to mine\n")
print("[yellow]Selected unit: ")
print(UNIT)

if (cell1[0] == 1) {
    x = cell1[1]
    y = cell1[2]

    print("\n[green]Coordinates set\n X: ")
    print(x)
    print("\n Y: ")
    print(y)
} else {
    print("\n[red]Coordinates not set")
}
printflush(message1)

if (switch1.enabled) {
    Unit player = radar(player, ally, any, distance, hail1, 1)

    if (player.shooting) {
        x = floor(player.shootX + 0.5)
        y = floor(player.shootY + 0.5)

        cell1[0] = 1
        cell1[1] = x
        cell1[2] = y

        switch1.enabled = false

        ubind(UNIT)
        Unit first = @unit
        while (true) {
            if (@unit.totalItems > 0) {
                ucontrol.flag(DEPOSIT_FLAG)
            }
            ubind(UNIT)
            if (@unit == first) {
                break
            }
        }
    }

    end
}

if (cell1[0] == 0) {
    end
}

ubind(UNIT)
if (@unit.flag == DEPOSIT_FLAG) {
    if (VAULT === null) {
        ulocate(building, core, false, _, cx, cy, cf, core)
        if (cf) {
            ucontrol.approach(cx, cy, 4)
            if (ucontrol.within(cx, cy, 6)) {
                ucontrol.itemDrop(core, @unit.itemCapacity)
                ucontrol.flag(0)
                ucontrol.approach(x, y, 4)
            }
        }
    } else {
        num vx = VAULT.x
        num vy = VAULT.y
        ucontrol.approach(vx, vy, 4)
        if (ucontrol.within(vx, vy, 6)) {
            ucontrol.itemDrop(VAULT, @unit.itemCapacity)
            ucontrol.flag(0)
            ucontrol.approach(x, y, 4)
        }
    }
} else {
    ucontrol.approach(x, y, 4)
    ucontrol.mine(x, y)

    if (@unit.totalItems == @unit.itemCapacity) {
        ucontrol.flag(DEPOSIT_FLAG)
    }
}
